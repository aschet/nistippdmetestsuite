$$ This file assumes that the positive Z axis of the machine is
$$ perpendicular to the table of the machine and points away from
$$ the table. If this is not the case, do not use this program.

$$ The D(PARTX) line below should be modified as follows.
$$ Set the number after ZORIG to the Z value at the surface of
$$ the table. Set the numbers after XORIG and YORIG to a convenient
$$ location near the middle of the table.

$$ Place the part with the large flat side on the table and the bottom
$$ of the large hole centered at the point whose coordinates are
$$ (XORIG_number, YORIG_number, ZORIG_number). Align the long edges of
$$ the part parallel to the X axis of the machine so that the end with
$$ the two spheres is on the positive X side.

DMISMN/'IMTS_M_clean.dmi', 4.0
DISPLY/TERM, DMIS
FILNAM/ 'IMTS DMIS output', 4.0

UNITS/MM,ANGDEC
PRCOMP/ON
SNSET/DEPTH,0.000

D(MCS1)=DATSET/MCS
DID(OUTFILE)=DEVICE/STOR,'C:\imts.dmo'
OPEN/DID(OUTFILE),FDATA, DMIS, OUTPUT

$$ The following 3 lines are machine specific and may need to be changed. 
SNSMNT/XVEC, 0.0, -1.0, 0.0, ZVEC, 0.0, 0.0, -1.0, MNTLEN, 0.0, 0.0, -175.1
S(PROBE6)=SNSDEF/PROBE, INDEX, POL, 0.0, 0.0, 0.0, 0.0, -1.0, 50, 4.0
SNSLCT/S(PROBE6)

SNSET/APPRCH,4.000
SNSET/SEARCH,9.000
SNSET/RETRCT,5.000
MODE/PROG,MAN
$$ Warning!  Change numbers on the next line as described above.
D(PARTX) = TRANS/XORIG,355.0,YORIG,91.0,ZORIG,-80.0

GOTO/-43.000,15.000,100.000

F(PLN1)=FEAT/PLANE,CART,0.000,0.000,30.000,0.000,0.000,1.000
T(TOL1)=TOL/FLAT,0.050

MEAS/PLANE,F(PLN1),4
  GOTO/-43.000,15.000,55.000
  PTMEAS/CART,-43.000,+15.000,30.000,0.000,0.000,1.000
  PTMEAS/CART,+43.000,+15.000,30.000,0.000,0.000,1.000
  PTMEAS/CART,+43.000,-35.000,30.000,0.000,0.000,1.000
  PTMEAS/CART,-50.000,-25.000,30.000,0.000,0.000,1.000
  GOTO/-50.000,-35.000,100.000
ENDMES
DATDEF/FA(PLN1),DAT(A)
OUTPUT/FA(PLN1),TA(TOL1)

GOTO/21.000,-62.000,100.000

F(PLN2)=FEAT/PLANE,CART,0.000,-52.000,15.000,0.000,-1.000,0.000

MEAS/PLANE,F(PLN2),4
  GOTO/19.000,-62.000,23.000
  PTMEAS/CART,+19.000,-52.000,23.000,0.000,-1.000,0.000
  PTMEAS/CART,+19.000,-52.000, 7.000,0.000,-1.000,0.000
  PTMEAS/CART,-19.000,-52.000, 7.000,0.000,-1.000,0.000
  PTMEAS/CART,-19.000,-52.000,23.000,0.000,-1.000,0.000
  GOTO/-19.000,-62.000,100.000
ENDMES
DATDEF/FA(PLN2),DAT(B)
OUTPUT/FA(PLN2)

GOTO/-96.000,-14.000,100.000

F(PLN3)=FEAT/PLANE,CART,-86.000,-20.000,15.000,-1.000,0.000,0.000

MEAS/PLANE,F(PLN3),4
  GOTO/-96.000,-14.000,23.000
  PTMEAS/CART,-86.000,-14.000,23.000,-1.000,0.000,0.000
  PTMEAS/CART,-86.000,-29.000,23.000,-1.000,0.000,0.000
  PTMEAS/CART,-86.000,-29.000, 7.000,-1.000,0.000,0.000
  PTMEAS/CART,-86.000,-14.000, 7.000,-1.000,0.000,0.000
  GOTO/-96.000,-14.000,100.000
ENDMES
DATDEF/FA(PLN3),DAT(C)
OUTPUT/FA(PLN3)

F(CLIN1)=FEAT/LINE,UNBND,CART,0.000,-52.000,30.000,1.000,0.000,0.000, $
0.000,0.000,1.000
CONST/LINE,F(CLIN1),INTOF,FA(PLN1),FA(PLN2)
OUTPUT/FA(CLIN1)

F(CPNT1)=FEAT/POINT,CART,-86.000,-52.000,30.000,-1.000,0.000,0.000
CONST/POINT,F(CPNT1),INTOF,FA(CLIN1),FA(PLN3)
OUTPUT/FA(CPNT1)

DATDEF/FA(PLN1),DAT(CALN1A)
D(CALN1_a)=DATSET/DAT(CALN1A),ZDIR
D(CALN1_b)= ROTATE/ZAXIS,FA(CLIN1),XDIR
D(CALN1_c)= TRANS/XORIG,FA(CPNT1)
D(CALN1_e)= TRANS/YORIG,FA(CPNT1)
D(CALN1_q)= TRANS/ZORIG,FA(CPNT1)
D(CALN1_t)=ROTATE/ZAXIS,-180.000
D(CALN1_u)=TRANS/XORIG,-86.000
D(CALN1_v)=TRANS/YORIG,-52.000
D(CALN1)=TRANS/ZORIG,-30.000
SAVE/DA(CALN1)

F(CYL1)=FEAT/CYLNDR,INNER,CART,0.00,0.00,26.00,0.00,0.00,-1.000,31.000, 12.000
T(TOL2)=TOL/POS,3D,0.100,RFS,DAT(A),DAT(B),DAT(C)
T(TOL3)=TOL/POS, 2D,0.000, RFS
T(TOL4)=TOL/DIAM,-0.100,0.100

MEAS/CYLNDR,F(CYL1),8
  GOTO/0.000,-9.500,51.000
  PTMEAS/CART,0.000,-15.500,23.000,0.000,1.000,0.000
  PTMEAS/CART,-15.500,-0.000,23.000,1.000,0.000,0.000
  PTMEAS/CART,-0.000,15.500,23.000,0.000,-1.000,0.000
  PTMEAS/CART,15.500,0.000,23.000,-1.000,-0.000,0.000
  PTMEAS/CART,15.500,0.000,17.000,-1.000,-0.000,0.000
  PTMEAS/CART,-0.000,15.500,17.000,0.000,-1.000,0.000
  PTMEAS/CART,-15.500,-0.000,17.000,1.000,0.000,0.000
  PTMEAS/CART,0.000,-15.500,17.000,0.000,1.000,0.000
  GOTO/0.000,-10.500,46.000
ENDMES
TEXT/OUTFIL,'CYL1 - CYLINDER - '
OUTPUT/FA(CYL1),TA(TOL2), TA(TOL3), TA(TOL4)

F(CYL2)=FEAT/CYLNDR,INNER,CART,0.00,0.00,0.00,0.00,0.00,1.00,12.500, 14.000
T(TOL5)=TOL/CYLCTY,0.100
T(TOL6)=TOL/POS, 2D,0.000, RFS
T(TOL7)=TOL/DIAM,-0.100,0.100

MEAS/CYLNDR,F(CYL2),8
  GOTO/0.000,0.250,39.000
  PTMEAS/CART,0.000,6.250,3.000,0.000,-1.000,0.000
  PTMEAS/CART,-6.250,0.000,3.000,1.000,-0.000,0.000
  PTMEAS/CART,-0.000,-6.250,3.000,0.000,1.000,0.000
  PTMEAS/CART,6.250,-0.000,3.000,-1.000,0.000,0.000
  PTMEAS/CART,6.250,-0.000,11.000,-1.000,0.000,0.000
  PTMEAS/CART,-0.000,-6.250,11.000,0.000,1.000,0.000
  PTMEAS/CART,-6.250,0.000,11.000,1.000,-0.000,0.000
  PTMEAS/CART,0.000,6.250,11.000,0.000,-1.000,0.000
  GOTO/0.000,1.250,34.000
ENDMES
TEXT/OUTFIL,'CYL2 - CYLINDER - '
OUTPUT/FA(CYL2),TA(TOL5),TA(TOL6),TA(TOL7)

GOTO/0.000,1.250,200.000
GOTO/-200.000,-62.000,200.000
CLOSE/DID(OUTFILE)
ENDFIL